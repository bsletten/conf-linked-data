import org.netkernel.rdf.jena.rep.*;
import com.hp.hpl.jena.rdf.model.*;
import com.hp.hpl.jena.vocabulary.*;
import com.hp.hpl.jena.shared.*;
import com.hp.hpl.jena.datatypes.xsd.*;	



def renderValue(host, node) {
	def rendered
		
	// TODO: Move this into a class
	def nsMap = [
		"http://www.w3.org/2001/XMLSchema" : "xsd",
	    "http://www.w3.org/2000/01/rdf-schema#" : "rdfs",
		(RDF.getURI().toString()) : "rdf",
		(DC_11.getURI().toString()) : "dc",
		"http://www.w3.org/ns/dcat#" : "dcat",
		"http://purl.org/dc/terms/" : "dcterms",
		"http://xmlns.com/foaf/0.1/" : "foaf",
		"http://www.w3.org/2002/12/cal/icaltzd/" : "cal",
		"http://purl.org/net/biblio#" : "biblio",
		"http://swrc.ontoware.org/ontology#" : "swrc",
		"http://data.semanticweb.org/ns/swc/ontology#" : "swc",
		"http://www.w3.org/2004/02/skos/core#" : "skos",
		(VCARD.getURI().toString()) : "vcard"
	]	
		
	if(node.isURIResource()) {
		def nodeRes = node.asResource()
		def uri = nodeRes.getURI()
		def ns = nodeRes.getNameSpace().toString()
		def shownURI = uri
		def nsPrefix = nsMap[ns]
			
		if(nsPrefix) {
			shownURI = shownURI.replaceAll(ns, "${nsPrefix}:")
		}
		
		def linkURI = uri
			
		if(host.startsWith("localhost")) {
			linkURI = uri.replaceAll("http://data.semanticweb.com", "http://${host}")
		}
		
		rendered = """<a href=\"${linkURI}\">${shownURI}</a>"""
	} else {
		rendered = node.toString()
	}
	
	rendered
}

def newAddStatements(host, sb, model, res, stmtItorList) {
	
	def resMap = new TreeMap()
		
	def resSortedMap
		
	stmtItorList.each { itor ->
		itor.each { st ->
			def subj = st.getSubject()
			def pred = st.getPredicate()
			def obj = st.getObject()
				
			resSortedMap = resMap[subj.getURI()]
				
			if(!resSortedMap) {
				resSortedMap = new TreeMap()
				resMap[subj.getURI()] = resSortedMap
			}
			
			def valList = resSortedMap[pred.getURI()]
			
			if(!valList) {
				valList = []
				resSortedMap[pred.getURI()] = valList
			}
			valList.add(obj)
		}
    }
	
	resSortedMap = resMap[res.getURI()]
		
	// We want RDFS Label and RDF Type properties at the top
	
	def rdfTypeProp = model.getProperty("http://www.w3.org/1999/02/22-rdf-syntax-ns#type")
	def rdfsLabelProp = model.getProperty("http://www.w3.org/2000/01/rdf-schema#label") 	
	
	[rdfsLabelProp, rdfTypeProp].each { p ->
		resSortedMap[p.getURI()].each { v ->
			sb.append("<tr>")
			sb.append("<td>${renderValue(host, p)}</td><td>${renderValue(host, v)}</td>")			
			sb.append("</tr>")							
		}
		
		resSortedMap.remove(p.getURI())
	}
	
	// Then do the other properties sorted by predicate

	resSortedMap.each { k, vList ->
		vList.sort{ it.toString() }.each { v ->
			def propNode = model.getResource(k)		
			sb.append("<tr>")
			sb.append("<td>${renderValue(host, propNode)}</td><td>${renderValue(host, v)}</td>")				
			sb.append("</tr>")				
		}
	}
	
	resMap.remove(res.getURI())
		
	// Then do the object relationships sorted by subject
		
	resMap.each { r, m ->
		def s = model.getResource(r)
			
		m.each { k, vList ->
			def p = model.getResource(k)
				
			sb.append("<tr>")
			sb.append("<td>is <a href='${p.getURI()}'>${renderValue(host, p)}</a> of </td><td>${renderValue(host, s)}</td>")
			sb.append("</tr>")				
		}
	}
}

def host = context.source("httpRequest:/header/Host")
def resource = context.source("arg:resource");
def resourceURI = "http://data.semanticweb.com/resource/${resource}".toString()
	
def req = context.createRequest("active:dataversity-conferences")
def model = context.issueRequest(req)
	
def jenaModel = context.source("active:jRDFEmptyModel").getModel()
	
req = context.createRequest("active:jRDFQueryModelDESCRIBE")
req.addArgumentByValue("operand", model)
req.addArgumentByValue("operator", "DESCRIBE <${resourceURI}>".toString())
resp = context.issueRequest(req)
	
jenaModel.add(resp.getModel())

req = context.createRequest("active:jRDFQueryModelCONSTRUCT")
req.addArgumentByValue("operand", model)
req.addArgumentByValue("operator", "CONSTRUCT { ?s ?p <${resourceURI}>} WHERE { ?s ?p <${resourceURI}> }".toString())
resp = context.issueRequest(req)

jenaModel.add(resp.getModel())	

def resourceRes = jenaModel.getResource(resourceURI)

def sb = new StringBuffer("<tbody>")
	
// Find all the statements about this resource or where it is the object

newAddStatements(host, sb, jenaModel, resourceRes, 
	[ jenaModel.listStatements(new SimpleSelector(resourceRes, null, null)), 
	  jenaModel.listStatements(new SimpleSelector(null, null, resourceRes))])

sb.append("</tbody>")
	
context.createResponseFrom(sb.toString())